import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: "com.google.cloud.artifactregistry.gradle-plugin"
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    defaultConfig {
        applicationId "com.kodabots.sdk.sample"
        namespace "com.kodabots.sdk.sample"
        minSdk 23
        compileSdk 35
        targetSdk 35
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders["KODA_AI_CLIENT_TOKEN"] = properties.get("KODA_AI_CLIENT_TOKEN")
    }

    buildTypes {
        release {
            resValue "string", "app_name", "KODA.AI"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable false
            matchingFallbacks = 'release'
        }
        debug {
            resValue "string", "app_name", "KODA.AI Debug"
            applicationIdSuffix '.debug'
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = 'debug'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        stage {
            resValue "string", "app_name", "KODA.AI Staging"
            applicationIdSuffix '.stage'
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = 'stage'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "KodaBotsSDKSample_${variant.name.capitalize()}_${getWorkingBranch().toUpperCase().replace("FEATURE/", "")}_${variant.versionName}_VC${variant.versionCode}_${getDateTime()}.apk"
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":core")
    implementation libs.kotlin.stdlib
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    implementation libs.google.material
    implementation libs.androidx.cardview
    implementation libs.androidx.constraintlayout

    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android


    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.espresso.core
}

static def getCommitDate() {
    return "git show -s --format=%ci".execute().text.trim()
}

static def getGitHash() {
    return "git rev-parse --short HEAD".execute().text.trim()
}

static def getWorkingBranch() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("yyyyMMddHHmm");
    return df.format(new Date());
}
