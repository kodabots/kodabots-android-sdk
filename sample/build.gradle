import java.text.DateFormat
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.kodabots.sdk.sample"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            resValue "string", "app_name", "KodaBotsSample"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable true
            matchingFallbacks = 'release'
        }
        debug {
            resValue "string", "app_name", "KodaBotsSample Debug"
            applicationIdSuffix '.debug'
            minifyEnabled true
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = 'debug'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        stage {
            resValue "string", "app_name", "KodaBotsSample Stage"
            applicationIdSuffix '.stage'
            minifyEnabled true
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = 'stage'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "KodaBotsSDKSample_${variant.name.capitalize()}_${getWorkingBranch().toUpperCase().replace("FEATURE/", "")}_${variant.versionName}_VC${variant.versionCode}_${getDateTime()}.apk"
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":core")
//    implementation 'com.kodabots.sdk:kodabots-core:1.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'pub.devrel:easypermissions:3.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}

def getCommitDate() {
    return "git show -s --format=%ci".execute().text.trim()
}

def getGitHash() {
    return "git rev-parse --short HEAD".execute().text.trim()
}

def getWorkingBranch() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

def getDateTime() {
    DateFormat df = new SimpleDateFormat("yyyyMMddHHmm");
    return df.format(new Date());
}
